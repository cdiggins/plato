library Array
{
    Map(xs: Array, f: Function): Array 
        => (xs.Count, (i: Integer) => f(xs.At(i)));

    Reverse(xs: Array): Array
        => (xs.Count, (i: Integer) => f(xs.At(xs.Count - 1 - i)));

    Zip(xs: Array, ys: Array, f: Function): Array 
        => (xs.Count, (i: Integer) => f(At(i), ys.At(i)));
    
    Zip(xs: Array, ys: Array, zs: Array, f: Function): Array 
        => (xs.Count, (i: Integer) => f(At(i), ys.At(i), zs.At(i)));
    
    Skip(xs: Array, n: Integer): Array
        => (Count - n, (i: Integer) => At(i - n));
    
    Take(xs: Array, n: Integer): Array
        => (n, (i: Integer) => At(i));   
    
    Aggregate(xs: Array, init: Any, f: Function): Any 
        => xs.IsEmpty ? init : f(init, f(xs.Rest));

    Rest(xs: Array): Array 
        => xs.Skip(1);
    
    IsEmpty(xs: Array): Boolean
        => xs.Count == 0;
    
    First(xs: Array): Any 
        => xs.At(0);
    
    Last(xs: Array): Any
        => xs.At(xs.Count - 1);
    
    Slice(xs: Array, from: Integer, count: Integer): Array 
        => xs.Skip(from).Take(count);
    
    Join(xs: Array, sep: String): String 
        => xs.IsEmpty ? "" 
            : xs.First.ToString 
            + xs.Rest.Aggregate("", (acc: String, cur: Any) => $"{acc}{sep}{cur}");
    
    All(xs: Array, f: Function): Boolean 
        => xs.IsEmpty ? true : f(xs.First) && f(xs.Rest);

    All(xs: Array): Boolean 
        => xs.All((b: Boolean) => b);
} 

library Interval
{
    Size(x: Interval): Numerical 
        => x.Max - x.Min;
    
    IsEmpty(x: Interval): Boolean 
        => x.Min >= x.Max;
    
    Lerp(x: Interval, amount: Unit): Numerical 
        => x.Min * (1.0 - amount) + x.Max * amount;
    
    InverseLerp(x: Interval, value: Numerical): Unit 
        => (value - x.Min) / x.Size;
    
    Negate(x: Interval): Interval 
        => (-x.Max, -x.Min);
    
    Reverse(x: Interval): Interval 
        => (x.Max, x.Min);
        
    Center(x: Interval): Numerical
        => x.Lerp(0.5);    
    
    Contains(x: Interval, value: Numerical): Boolean 
        => x.Min <= value && value <= x.Max;
    
    Contains(x: Interval, other: Interval): Boolean 
        => x.Min <= other.Min && Max >= other.Max;
    
    Overlaps(x: Interval, y: Interval): Boolean 
        => !x.Clamp(y).IsEmpty;
    
    Split(x: Interval, t: Unit): Tuple 
        => (x.Left(t), x.Right(t));
    
    Split(x: Interval): Tuple
        => x.Split(0.5);

    Left(x: Interval, t: Unit): Interval
        => (x.Min, x.Lerp(t));
    
    Right(x: Interval, t: Unit): Interval
        => (x.Lerp(t), x.Max);

    MoveTo(x: Interval, v: Numerical): Interval
        => (v, v + x.Size);
    
    LeftHalf(x: Interval): Interval 
        => x.Left(0.5);
    
    RightHalf(x: Interval): Interval 
        => x.Right(0.5);
    
    HalfSize(x: Interval): Numerical 
        => x.Size.Half;
    
    Recenter(x: Interval, c: Numerical): Interval 
        => (c - x.HalfSize, c + x.HalfSize);
    
    Clamp(x: Interval, y: Interval): Interval
        => (x.Clamp(y.Min), x.Clamp(y.Max));
    
    Clamp(x: Interval, value: Numerical): Numerical 
        => value < x.Min ? x.Min : value > x.Max ? x.Max : value;
    
    Within(x: Interval, value: Numerical): Boolean
        => value >= x.Min && value <= x.Max;
}

library Value
{ 
    ToString(x: Value): String
        => x.FieldValues.Join(", ");
}

library Vector
{
    Sum(v: Array): Numerical 
        => v.Aggregate(0, Add); 
    
    SumSquares(v: Array): Number
        => v.Square.Sum; 
    
    LengthSquared(v: Array): Number
        => v.SumSquares;
    
    Length(v: Array): Number
        => v.LengthSquared.SquareRoot;
    
    Dot(v1: Vector, v2: Vector): Number
        => (v1 * v2).Sum;

    Normal(v: Vector): Vector
        => v / v.Length;
}

library Numerical
{
    SquareRoot(x: Number): Number 
        => x.Pow(0.5);

    Square(x: Number): Number 
        => x * x;

    Clamp(x: Number): Number 
        => Clamp(x, (0, 1));

    PlusOne(x: Number): Number 
        => x + x.One;

    MinusOne(x: Number): Number
        => x - x.One;
    
    FromOne(x: Number): Number 
        => x.One - x;

    IsPositive(x: Number): Boolean
        => x >= 0;

    GtZ(x : Number): Boolean
        => x > 0;

    LtZ(x : Number): Boolean
        => x < 0;

    GtEqZ(x : Number): Boolean
        => x >= 0;

    LtEqZ(x : Number): Boolean
        => x <= 0;

    IsNegative(x: Number): Boolean
        => x < 0;

    Sign(x: Number): Number 
        => x.LtZ ? -x.One : x.GtZ ? x.One : x.Zero;
    
    Abs(x: Number): Number 
        => x.LtZ ? -x: x;
    
    Half(x: Number): Number 
        => x / 2;
    
    Third(x: Number): Number 
        => x / 3;
    
    Quarter(x: Number): Number 
        => x / 4;

    Fifth(x: Number): Number 
        => x / 5;
    
    Sixth(x: Number): Number 
        => x / 6;
    
    Seventh(x: Number): Number 
        => x / 7;
    
    Eighth(x: Number): Number 
        => x / 8;
    
    Ninth(x: Number): Number 
        => x / 9;
    
    Tenth(x: Number): Number 
        => x / 10;
    
    Sixteenth(x: Number): Number 
        => x / 16;
    
    Hundredth(x: Number): Number 
        => x / 100;
    
    Thousandth(x: Number): Number 
        => x / 1000;
    
    Millionth(x: Number): Number 
        => x / 1000 / 1000;
    
    Billionth(x: Number): Number 
        => x / 1000 / 1000 / 1000;
    
    Hundred(x: Number): Number 
        => x * 100;
    
    Thousand(x: Number): Number
        => x * 1000;
    
    Million(x: Number): Number
        => x * 1000 * 1000;
    
    Billion(x: Number): Number 
        => x * 1000 * 1000 * 1000;
    
    Twice(x: Number): Number 
        => x * 2;
    
    Thrice(x: Number): Number 
        => x * 3;
    
    SmoothStep(x: Number): Number 
        => x.Square * (3 - x.Twice);
    
    Pow2(x: Number): Number 
        => x * x;
    
    Pow3(x: Number): Number 
        => x.Pow2 * x;

    Pow4(x: Number): Number 
        => x.Pow3 * x;

    Pow5(x: Number): Number 
        => x.Pow4 * x;

    Pi(): Number 
        => 3.1415926535897;
    
    AlmostZero(x: Number): Boolean
        => x.Abs < 0.00000001; 

    Lerp(a: Number, b: Number, t: Unit): Number
        => (1 - t) * a + t * b;
        
    Between(self: Number, min: Number, max: Number): Boolean
        => self.FieldValues.Zip(min.FieldValues, max.FieldValues, 
            (x: Number, y: Number, z: Number) => Between(x, y, z));
}

library Angles 
{
    Radians(x : Number): Angle
        => x;

    Degrees(x: Number): Angle
        => x * Pi / 180; 
        
    Turns(x: Number): Angle 
        => x * 2 * Pi;    
}

library Comparable
{
    Equals(a: Comparable, b: Comparable): Boolean 
        => a.Compare(b) == 0;
    
    LessThan(a: Comparable, b: Comparable): Boolean 
        => a.Compare(b) < 0;
    
    LessThanOrEquals(a: Comparable, b: Comparable): Boolean
        => a.Compare(b) <= 0;
    
    GreaterThan(a: Comparable, b: Comparable): Boolean 
        => a.Compare(b) > 0;
    
    GreaterThanOrEquals(a: Comparable, b: Comparable): Boolean 
        => a.Compare(b) >= 0;
    
    Between(v: Comparable, a: Comparable, b: Comparable): Value 
        => v >= a && v <= b;
    
    Between(v: Value, i: Interval): Interval 
        => i.Contains(v);
    
    Min(a: Comparable, b: Comparable): Comparable 
        => a <= b ? a : b;
    
    Max(a: Comparable, b: Comparable): Comparable 
        => a >= b ? a : b;
}

library Equatable
{
    NotEquals(x: Equatable, y: Equatable): Boolean
        => !x.Equals(y);
}

library Easings
{
    //===
    // Easings.cs
    // https://easings.net/
    // https://github.com/acron0/Easings/blob/master/Easings.cs
    // https://github.com/ai/easings.net/blob/master/src/easings/easingsFunctions.ts
    // This is an extremely good example of why Plato matters as a language. 
    // When we look at the source code of the two other implementations. 

    BlendEaseFunc(p: Number, easeIn: Function, easeOut: Function): Number 
        => p < 0.5 
            ? 0.5 * easeIn(p * 2) 
            : 0.5 * easeOut(p * 2 - 1) + 0.5;
    
    InvertEaseFunc(p: Number, easeIn: Function): Number 
        => 1 - easeIn(1 - p);

    Linear(p: Number): Number 
        => p;

    QuadraticEaseIn(p: Number): Number 
        => p.Pow2;
    
    QuadraticEaseOut(p: Number): Number
        => InvertEaseFunc(p, QuadraticEaseIn);
    
    QuadraticEaseInOut(p: Number): Number
        => BlendEaseFunc(p, QuadraticEaseIn, QuadraticEaseOut);
    
    CubicEaseIn(p: Number): Number 
        => p.Pow3;
    
    CubicEaseOut(p: Number): Number
        => InvertEaseFunc(p, CubicEaseIn);
    
    CubicEaseInOut(p: Number): Number 
        => BlendEaseFunc(p, CubicEaseIn, CubicEaseOut);
    
    QuarticEaseIn(p: Number): Number
        => p.Pow4;
    
    QuarticEaseOut(p: Number): Number 
        => InvertEaseFunc(p, QuarticEaseIn);
    
    QuarticEaseInOut(p: Number): Number 
        => BlendEaseFunc(p, QuarticEaseIn, QuarticEaseOut);
    
    QuinticEaseIn(p: Number): Number 
        => p.Pow5;
    
    QuinticEaseOut(p: Number): Number 
        => InvertEaseFunc(p, QuinticEaseIn);
    
    QuinticEaseInOut(p: Number): Number
        => BlendEaseFunc(p, QuinticEaseIn, QuinticEaseOut);
    
    SineEaseIn(p: Number): Number
        => InvertEaseFunc(p, SineEaseOut);

    SineEaseOut(p: Number): Number 
        => p.Quarter.Turns.Sin;
    
    SineEaseInOut(p: Number): Number
        => BlendEaseFunc(p, SineEaseIn, SineEaseOut);
    
    CircularEaseIn(p: Number): Number 
        => p.Pow2.FromOne.SquareRoot.FromOne;
    
    CircularEaseOut(p: Number): Number 
        => InvertEaseFunc(p, CircularEaseIn);
    
    CircularEaseInOut(p: Number): Number 
        => BlendEaseFunc(p, CircularEaseIn, CircularEaseOut);
    
    ExponentialEaseIn(p: Number): Number 
        => p.AlmostZero ? p : 2.Pow(10 * p.MinusOne);
    
    ExponentialEaseOut(p: Number): Number 
        => InvertEaseFunc(p, ExponentialEaseIn);
    
    ExponentialEaseInOut(p: Number): Number 
        => BlendEaseFunc(p, ExponentialEaseIn, ExponentialEaseOut);
    
    ElasticEaseIn(p: Number): Number 
        => 13 * p.Quarter.Turns * 2.Pow(10 * p.MinusOne).Radians.Sin;
    
    ElasticEaseOut(p: Number): Number 
        => InvertEaseFunc(p, ElasticEaseIn);
    
    ElasticEaseInOut(p: Number): Number 
        => BlendEaseFunc(p, ElasticEaseIn, ElasticEaseOut);
    
    BackEaseIn(p: Number): Number 
        => p.Pow3 - p * p.Half.Turns.Sin;
    
    BackEaseOut(p: Number): Number 
        => InvertEaseFunc(p, BackEaseIn);
    
    BackEaseInOut(p: Number): Number 
        => BlendEaseFunc(p, BackEaseIn, BackEaseOut);
    
    BounceEaseIn(p: Number): Number 
        => InvertEaseFunc(p, BounceEaseOut);
    
    BounceEaseOut(p: Number): Number 
        => (p < 4 / 11.0) ? 121.0 * p.Pow2 / 16.0
        : (p < 8 / 11.0) ? 363.0 / 40.0 * p.Pow2 - 99.0 / 10.0 * p + 17.0 / 5.0
        : (p < 9 / 10.0) ? 4356.0 / 361.0 * p.Pow2 - 35442.0 / 1805.0 * p + 16061.0 / 1805.0
        : 54.0 / 5.0 * p.Pow2 - 513.0 / 25.0 * p + 268.0 / 25.0;
    
    BounceEaseInOut(p: Number): Number
        => BlendEaseFunc(p, BounceEaseIn, BounceEaseOut);
}
