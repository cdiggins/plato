library PolarFunctions
{
    // https://en.wikipedia.org/wiki/Polar_coordinate_system
    Cartesian(polar: PolarCoordinate): Vector2D
        => Circle(polar.Angle) * polar.Radius;

    // https://en.wikipedia.org/wiki/Lima%C3%A7on
    PolarLimacon(phi: Angle, a: Number, b: Number): PolarCoordinate
        => (b + a * phi.Cos, phi);

    // https://en.wikipedia.org/wiki/Rose_(mathematics)        
    PolarRose(phi: Angle, a: Number, k: Number, c: Number): PolarCoordinate
        => (a * (k * phi + c).Cos, phi);

    // https://en.wikipedia.org/wiki/Archimedean_spiral
    ArchimedeanSpiral(phi: Angle, a: Number, b: Number): PolarCoordinate
        => (a + b * phi, phi);
                
    // https://en.wikipedia.org/wiki/Cardioid
    Cardoid(phi: Angle): PolarCoordinate
        => PolarLimacon(phi, 1, 1);
       
    // https://en.wikipedia.org/wiki/Conic_section
    ConicSection(phi: Angle, eccentricity: Number, semiLatusRectum: Number): PolarCoordinate
        => semiLatusRectum / (1 - eccentricity * phi.Cos);

    // https://en.wikipedia.org/wiki/Poinsot%27s_spirals
    PoinsotSpiralHyperbolicCosecant(phi: Angle, a: Number, n: Number): PolarCoordinate
        => (a * (n * phi).Csch, phi);

    // https://en.wikipedia.org/wiki/Poinsot%27s_spirals
    PoinsotSpiralHyperbolicSecant(phi: Angle, a: Number, n: Number): PolarCoordinate
        => (a * (n * phi).Sech, phi);
}

