library Angles 
{
    Radians(x: Number): Angle
        => x;

    Degrees(x: Number): Angle
        => (x / 360).Turns; 
        
    ArcMinutes(x: Number): Angle 
        => x.Degrees / 60;

    ArcSeconds(x: Number): Angle 
        => x.ArcMinutes / 60;

    Grads(x: Number): Angle 
        => (x / 400).Turns;
     
    Turns(x: Number): Angle 
        => x * TwoPi;    

    Degrees(x: Angle): Number
        => x.Turns * 360;

    Turns(x: Angle): Number
        => x / TwoPi;    

    ArcMinutes(x: Angle): Number
        => x.Degrees * 60;    

    ArcSeconds(x: Angle): Number
        => x.ArcMinutes * 60;    

    Grad(x: Angle): Number
        => x.Turns * 400;    

    //===
    // https://en.wikipedia.org/wiki/Hyperbolic_functions

    Sinh(x: Number): Number
        => (x.Exp - (-x).Exp) / 2;

    Cosh(x: Number): Number
        => (x.Exp + (-x).Exp) / 2;

    Tanh(x: Number): Number
        => x.Sinh / x.Cosh;

    Coth(x: Number): Number
        => x.Cosh / x.Sinh;

    Sech(x: Number): Number
        => x.Cosh.Inverse;

    Csch(x: Number): Number
        => x.Sinh.Inverse;

    //===
    // https://en.wikipedia.org/wiki/Trigonometric_functions

    Sec(x: Angle): Number
        => x.Cos.Inverse;

    Csc(x: Angle): Number
        => x.Cos.Inverse;

    Cot(x: Angle): Number
        => x.Tan.Inverse;

    //==
    // Complex plane trig: https://en.wikipedia.org/wiki/Trigonometric_functions#In_the_complex_plane

    Sin(c: Complex): Complex
        => (c.X.Sin * c.Y.Cosh, c.X.Cos * c.Y.Sinh);

    Cos(c: Complex): Complex
        => (c.X.Cos * c.Y.Cosh, c.X.Sin * c.Y.Sinh);
}
