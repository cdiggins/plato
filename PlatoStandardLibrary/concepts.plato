// https://en.wikipedia.org/wiki/Top_type
// This is the Top Type in Plato. All types provide this. 
// It enables more efficient reflection-type algorithms in C#. 
concept Any
{ 
    FieldNames(x: Self): Array<String>;     
    FieldValues(x: Self): Array<Any>;
}

// Values is a read-only type which has any only Values as members
concept Value 
    inherits Any, Equatable
{ 
}

// Arrays are immutable types which can be indexed. 
// The "At" function maps to the indexing operator in C#. 
concept Array<T: Any>
{
    Count(xs: Self): Integer;
    At(xs: Self, n: Integer): T;
}

// Vectors are numerical types which are arrays of numberical types. 
concept Vector<T: Numerical>
    inherits Array<T>, Numerical, Magnitudinal, Equatable, Coordinate
{
}

// A coordinate is a point in some cartesian space. This also includes 
// a time line, so a DateTime is considered a Coordinate. 
concept Coordinate
    inherits Interpolatable 
{
}

// A measure is a numeric value with an intrinisic unit of measure 
// which is implied by the implementing type. 
// It resembles a numerical type except you cannot multiply two measures without 
// getting a new type. It does however support ScalarArithmetic 
concept Measure
    inherits Value, ScalarArithmetic, Equatable, Comparable, Magnitudinal, Interpolatable
{ 
    Value(x: Self): Number;
}

// A numerical type represents things like floats, integers, complex numbers, and vectors. 
concept Numerical 
    inherits Value, Arithmetic, Equatable, Comparable, Magnitudinal, Interpolatable
{
    Zero(): Self;
    One(): Self;
    MinValue(): Self;
    MaxValue(): Self;
}

// Any type which has a numerical magnitude (like numbers, vectors, and measurements)
concept Magnitudinal
    inherits Comparable
{
    Magnitude(x: Self): Number;
}

// The compare function return a negative integer if x < y, a positive integer if x > y, or zero otherwise. 
concept Comparable
{
    Compare(x: Self, y: Self): Integer;
}

// Supported for when two values are equal 
concept Equatable
{
    Equals(a: Self, b: Self): Boolean;
    NotEquals(a: Self, b: Self): Boolean;
}

concept Arithmetic 
    inherits AdditiveArithmetic<Self>, MultiplicativeArithmetic<Self>, 
        AdditiveInverse, MultiplicativeInverse
{ 
}

concept AdditiveInverse
{
    Negative(self: Self): Self;
}

concept MultiplicativeInverse
{
    Reciprocal(self: Self): Self;
}

concept AdditiveArithmetic<T: Any>
{
    Add(self: Self, other: T): Self;    
    Subtract(self: Self, other: T): Self;
}

concept MultiplicativeArithmetic<T: Any>
{
    Multiply(self: Self, other: Self): Self;
    Divide(self: Self, other: Self): Self;    
    Modulo(self: Self, other: Self): Self;
}

concept ScalarArithmetic
    inherits AdditiveArithmetic<Number>, MultiplicativeArithmetic<Number>
{
}

concept BooleanOperations
{
    And(a: Self, b: Self): Self;
    Or(a: Self, b: Self): Self;    
    Not(a: Self): Self;
}

concept Interval<TValue: Interpolatable, TSize: Numerical>
    where TValue: AdditiveArithmetic<Size>
    where TSize: ScalarArithmetic 
{
    Min(x: Self): TValue;
    Max(x: Self): TValue;
    Size(x: Self): TSize;
}
    
concept Interpolatable
{
    Lerp(a: Self, b: Self, amount: Number): Self;
    Unlerp(v: Self, a: Self, b: Self): Number;
}